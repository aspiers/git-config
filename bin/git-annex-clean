#!/bin/zsh

# Run git annex while avoiding trying to touch ignored or
# unavailable remotes.

opts=()
to_ping=()
for remote in $( git remote ); do
    if git-remote-annex-is-ignored $remote; then
        # For some reason git-annex still tries to pull from ignored remotes
        opts+=( -c remote.$remote.url= )
        echo >&2 "$remote is ignored"
    elif git-should-ping-remote-annex $remote; then
        to_ping+=( $remote )
    fi
done

if which parallel >&/dev/null; then
    echo >&2 "Checking liveness of remotes in parallel: $to_ping"
    opts+=(
        $(
            echo $to_ping |
                parallel --trim=lr -d\  -n1 \
                         git-down-remote-ignore-opts {}
        )
    )
else
    for remote in $to_ping; do
        opts+=( $(git-down-remote-ignore-opts "$remote") )
    done
fi

echo git $opts[@] annex "$@"
git $opts[@] annex "$@"
